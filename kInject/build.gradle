apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'maven-publish'
apply plugin: 'org.jetbrains.dokka'

if (project.hasProperty("kotlin")) { //Kotlin libraries
    task sourcesJar(type: Jar) {
        classifier = 'sources'
        from android.sourceSets.main.java.srcDirs
    }

    task javadoc(type: Javadoc, dependsOn: dokka) {

    }
}else if (project.hasProperty("android")) { // Android libraries
    task sourcesJar(type: Jar) {
        classifier = 'sources'
        from android.sourceSets.main.java.srcDirs
    }

    task javadoc(type: Javadoc) {
        source = android.sourceSets.main.java.srcDirs
        classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
    }
} else { // Java libraries
    task sourcesJar(type: Jar, dependsOn: classes) {
        classifier = 'sources'
        from sourceSets.main.allSource
    }
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}


dokka {
    outputFormat = 'html'
    outputDirectory = "$buildDir/javadoc"
    configuration {
        externalDocumentationLink {
            noAndroidSdkLink = true
            url = new URL("https://github.com/ShowMeThe/Kinject/blob/master/package-list")
            packageListUrl = new URL("https://github.com/ShowMeThe/Kinject/blob/master/package-list")
        }
    }
}

tasks.withType(Javadoc) {
    options.addStringOption('Xdoclint:none', '-quiet')
    options.addStringOption('encoding', 'UTF-8')
    options.addStringOption('charSet', 'UTF-8')
}
artifacts {
    archives javadocJar
    archives sourcesJar
}
sourceSets {
    main.java.srcDirs += 'src/main/kotlin'
}



android {
    compileSdkVersion 29
    buildToolsVersion "29.0.3"

    defaultConfig {
        minSdkVersion 21
        targetSdkVersion 29
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles "consumer-rules.pro"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
}

dependencies {
    compileOnly fileTree(dir: "libs", include: ["*.jar"])
    compileOnly "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    compileOnly("androidx.lifecycle:lifecycle-common:2.4.1")
    compileOnly("androidx.lifecycle:lifecycle-runtime-ktx:2.4.1")
    compileOnly "org.jetbrains.kotlinx:kotlinx-coroutines-core:1.4.1"
    compileOnly "org.jetbrains.kotlinx:kotlinx-coroutines-android:1.4.1"
}


afterEvaluate {
    generateReleaseBuildConfig.enabled = false
    generateDebugBuildConfig.enabled = false
    publishing{
        publications {
            release(MavenPublication){
                from components.release
                groupId = "com.github.ShowMeThe"
                artifactId = "Kinject"
                version = "1.6.0"
                artifact sourcesJar
            }
        }
    }
}

//apply plugin: 'com.github.dcendents.android-maven'
//group='com.github.ShowMeThe'